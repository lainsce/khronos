# Name our project
project('com.github.lainsce.khronos', ['vala', 'c'],
    version: '2.0.0'
)

# Import main lib files
gnome = import('gnome')
i18n = import('i18n')

add_global_arguments([
        '-DGETTEXT_PACKAGE="@0@"'.format(meson.project_name()),
        '-DHANDY_USE_UNSTABLE_API'
    ],
    language:'c'
)

# Compile the GResources for this project
asresources = gnome.compile_resources(
    'as-resources', 'data/style.gresource.xml',
    source_dir: 'data',
    c_name: 'as'
)

# Set this for convenience
conf = configuration_data()

# Get the package data directory under Meson
pkgdatadir = join_paths([ get_option('prefix'),
                          get_option('datadir'),
                          'com.github.lainsce.khronos' ])

conf.set_quoted('PKGDATADIR', pkgdatadir)

# Other configs
conf
add_global_arguments('-DDATADIR="' + get_option('datadir') + '"', language: 'c')
configure_file(output: 'config.h', configuration: conf)
config_h_dir = include_directories('.')

# Arguments for C
c_args = [
  '-include', 'config.h',
  '-w'
]

cc = meson.get_compiler('c')
m_dep = cc.find_library('m', required: true)

# Let's define our executable
executable(
    meson.project_name(),
    'src/Application.vala',
    'src/MainWindow.vala',
    'src/Services/TaskManager.vala',
    'src/Services/FileManager.vala',
    'src/Services/Dialog.vala',
    'src/Widgets/Column.vala',
    'src/Widgets/ColumnListBox.vala',
    'src/Widgets/Box.vala',
    'src/Widgets/EventBox.vala',
    asresources,
    c_args: c_args,
    dependencies: [
        dependency('granite'),
        dependency('gobject-2.0'),
        dependency('json-glib-1.0'),
        dependency('gee-0.8'),
        dependency('gtk+-3.0'),
        dependency('libhandy-1', version: '>=0.80.0'),
        m_dep,
    ],
    vala_args: [
        meson.source_root() + '/vapi/config.vapi',
        '--vapidir=' + meson.source_root() + '/vapi/',
    ],
    install : true
)

# Add in a post install script
meson.add_install_script('meson/post_install.py', join_paths(get_option('prefix'), get_option('datadir')))

# Add subfolders for Meson to look
subdir('data')
subdir('po')
